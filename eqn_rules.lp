% Armando Diaz Tolentino <ajdt@cs.washington.edu> 
% 
% This file contains all the rules used to solve an equation
% generated by eqn_generator.lp
%
% each rule's section contains both the condtions for the rule to fire,
% and the actions to perform if the rule is fired.
%
% XXX: these rules no longer check the types of  N1, C1, and C2. Types are implicit depending on nodeDeg, nodeOper, etc... predicates that hold

%================================================================================
% addCommonTerms: a*x^b + c*x^b ----> (a+c)*x^b, 2 Cases
%================================================================================
% 						### RULE CONDITIONS ###
% there's an add poly node with two children that are monomials of the same degree
possible(action(data(N1, C1, C2, Coef1+Coef2), addCommTerms), S) 	:- 	holds(N1, nodeOper(add), S),
											holds(N1, parentOf(C1), S),
											holds(N1, parentOf(C2), S),
											holds(C1, nodeDeg(Deg), S),
											holds(C2, nodeDeg(Deg), S),
											holds(C1, nodeCoef(Coef1), S),
											holds(C2, nodeCoef(Coef2), S),
											Coef1 != 0,% remove
											Coef2 != 0,% remove
											C1 != C2.
% 						### APPLYING THE RULE ###
% 						### CASE 1: parent node has more than 2 children ###
% add the two nodes together
assigned(C1, nodeCoef(Sum), S) 			:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
											holds(N1, numChildren(Kids), S), Kids > 2.
% decrement number of children
assigned(N1, numChildren(Kids - 1), S) 	:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
											holds(N1, numChildren(Kids), S), Kids > 2.


% 						### CASE 2: parent node has exactly 2 children ###
%--------------------------------------------------------------------------------
% 								so parent node becomes monomial.
% TODO: cannot use inheritFrom here b/c coefficient isn't directly inherited. 
% remove old facts about parent
removed(N1, Fact, S) 				:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
										holds(N1, numChildren(Kids), S), Kids == 2,
										holds(C2, Fact, S).
% parent ID is still a node, becomes a monomial, and gains node parameters
assigned(N1, node(N1), S) 			:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
										holds(N1, numChildren(Kids), S), Kids == 2.
assigned(N1, type(mono), S) 		:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
										holds(N1, numChildren(Kids), S), Kids == 2.
assigned(N1, nodeCoef(Sum), S) 		:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
										holds(N1, numChildren(Kids), S), Kids == 2.
assigned(N1, nodeDeg(Degree), S) 	:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
										holds(N1, numChildren(Kids), S), Kids == 2,
										holds(C1, nodeDeg(Degree), S) .
% C1 child node is deleted 
removed(C1, Fact, S)				:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
										holds(N1, numChildren(Kids), S), Kids == 2,
										holds(C1, Fact, S).

%						### BOTH CASES ###
%--------------------------------------------------------------------------------
% completely erase second node											
removed(C2, Fact, S)	:-	_selected(action(data(N1, C1, C2, Sum), addCommTerms), S),
							holds(C2, Fact, S).
%================================================================================
%================================================================================


%================================================================================
% addIdentity: a*x^b + 0 ----> a*x^b 
%================================================================================
% 						### RULE CONDITIONS ###
% TODO: refactor action data from actions!
% there's an add poly node with two children that are monomials, one of them has coef == 0 
% the only info we remember is the identity of the parent node and the node to remove
possible(action(data(N1, C1, C2), addIdent), S) 	:- 	holds(N1, nodeOper(add), S),
											holds(N1, parentOf(C1), S),
											holds(N1, parentOf(C2), S),
											holds(C1, nodeCoef(Coef1), S),
											holds(C2, nodeCoef(0), S),
											C1 != C2.
% 						### APPLYING THE RULE ###
% 						### CASE 1: parent node has more than 2 children ###
% decrement number of children
assigned(N1, numChildren(Kids - 1), S) 	:-	_selected(action(data(N1, C1, C2), addIdent), S),
											holds(N1, numChildren(Kids), S), Kids > 2.


% 						### CASE 2: parent node has exactly 2 children ###
%--------------------------------------------------------------------------------

% parent inherits C1 child's properities
inheritFrom(N1, C1, S) :-	_selected(action(data(N1, C1, C2), addIdent), S), 
										holds(N1, numChildren(Kids), S), Kids == 2.
% C1 child node is deleted 
removed(C1, Fact, S)				:-	_selected(action(data(N1, C1, C2), addIdent), S),
										holds(N1, numChildren(Kids), S), Kids == 2,
										holds(C1, Fact, S).

%						### BOTH CASES ###
%--------------------------------------------------------------------------------
% completely erase second node											
removed(C2, Fact, S)	:-	_selected(action(data(N1, C1, C2), addIdent), S),
							holds(C2, Fact, S).
%================================================================================
%================================================================================

